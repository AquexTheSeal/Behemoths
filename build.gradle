buildscript {
    repositories {
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
        mavenCentral()
    }
    dependencies {
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}

plugins {
    id 'eclipse'
    id 'idea'
    id 'net.minecraftforge.gradle' version '[6.0.16,6.2]'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
    id "me.modmuss50.mod-publish-plugin" version "1.1.0"
}

apply plugin: 'org.spongepowered.mixin'

group = mod_group_id
version = mod_version

base {
    archivesName = mod_id
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(17)
}

minecraft {
    mappings channel: mapping_channel, version: mapping_version
    copyIdeResources = true

    runs {
        configureEach {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }

        client {
            args '--username', "ManMeme####"
            jvmArg "-XX:+AllowEnhancedClassRedefinition"
            jvmArg "-XX:HotswapAgent=fatjar"
            property 'forge.enabledGameTestNamespaces', mod_id
        }

        server {
            property 'forge.enabledGameTestNamespaces', mod_id
        }

        gameTestServer {
            property 'forge.enabledGameTestNamespaces', mod_id
        }

        data {
            workingDirectory project.file('run-data')
            args '--mod', mod_id, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
        }
    }
}

mixin {
    add sourceSets.main, "${mod_id}.refmap.json"
    config "${mod_id}.mixins.json"
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'

    compileOnly(annotationProcessor("io.github.llamalad7:mixinextras-common:${mixinextras_version}"))
    implementation(jarJar("io.github.llamalad7:mixinextras-forge:${mixinextras_version}")) {
        jarJar.ranged(it, "[${mixinextras_version},)")
    }

    implementation fg.deobf("curse.maven:jei-238222:7391695")
    implementation fg.deobf("curse.maven:blessfulled-1182590:6624691")
    implementation fg.deobf("curse.maven:emi-580555:6420945")

    compileOnly fg.deobf("curse.maven:better-combat-by-daedelus-639842:5625757")
}

repositories {
    mavenCentral()

    maven {
        name = "CurseMaven"
        url = "https://www.cursemaven.com"
    }

    maven {
        name = "BlameJared maven"
        url = "https://maven.blamejared.com/"
    }
}

tasks.named('processResources', ProcessResources).configure {
    var replaceProperties = [
        minecraft_version: minecraft_version,
        minecraft_version_range: minecraft_version_range,
        forge_version: forge_version,
        forge_version_range: forge_version_range,
        loader_version_range: loader_version_range,
        mod_id: mod_id,
        mod_name: mod_name,
        mod_license: mod_license,
        mod_version: mod_version,
        mod_authors: mod_authors,
        mod_description: mod_description,
    ]

    inputs.properties replaceProperties

    filesMatching(['META-INF/mods.toml', 'pack.mcmeta']) {
        expand replaceProperties + [project: project]
    }
}

tasks.register('buildIntoProduction', Copy) {
    dependsOn 'build'

    from(tasks.named('jarJar').flatMap { it.archiveFile })
    into(project.findProperty("CURSEFORGE_LAUNCHER_FOLDER") ?: providers.environmentVariable("CURSEFORGE_LAUNCHER_FOLDER").getOrElse(""))

    doFirst {
        println "Installing mod jar into CurseForge profile mods folder..."
    }
}

tasks.named('jar', Jar).configure {
    enabled = false
}

afterEvaluate {
    tasks.named('reobfJar').configure {
        enabled = false
    }
}

tasks.named('jarJar', Jar).configure {
    archiveClassifier = ""
    manifest {
        attributes([
                "Specification-Title"     : mod_id,
                "Specification-Vendor"    : mod_authors,
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : archiveVersion,
                "Implementation-Vendor"   : mod_authors,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

reobf {
    jarJar {}
}

publishMods {
    file = tasks.named('jarJar').get().archiveFile
    changelog = file("changelog/CL-${mod_version}.md").text
    type = STABLE
    modLoaders.add("forge")

    curseforge {
        projectId = "1454931"
        accessToken = project.findProperty("CURSEFORGE_TOKEN") ?: providers.environmentVariable("CURSEFORGE_TOKEN").getOrElse("")
        minecraftVersions.add(minecraft_version)
    }

    modrinth {
        projectId = "V2LHp4Si"
        accessToken = project.findProperty("MODRINTH_TOKEN") ?: providers.environmentVariable("MODRINTH_TOKEN").getOrElse("")
        minecraftVersions.add(minecraft_version)
    }
}